<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:components="components.*" destructionPolicy="never"
		add="viewAddHandler(event)" title="EditRecord">
	<fx:Declarations>
		<!-- 非ビジュアルエレメント (サービス、値オブジェクトなど) をここに配置 -->
	</fx:Declarations>
	<fx:Script>
	<![CDATA[
		import models.MainModel;
		
		import spark.events.TextOperationEvent;
		import spark.transitions.SlideViewTransition;
		import spark.transitions.SlideViewTransitionMode;
		import spark.transitions.ViewTransitionDirection;
		
		/* ファイルを保存するかどうか */
		private var saveFlag:Boolean = false;

		private function cancelButtonClickHandler(event:MouseEvent):void
		{
			saveFlag = false;
			popView();
		}
			
		private function doneButtonClickHandler(event:MouseEvent):void
		{
			// TODO:アプリ再開がこの画面から再開するときは保存ができない。まあAddでなくEditだから大した被害ないけど。
			if (data != null) // アプリ再開がこの画面のときはdataはnullになっている
			{
				// setCommentをsetRecordNameより先にする。
				// setRecordNameによりファイルの実体のファイル名が変わってしまい、dataの中のパスでは参照できなくなる。
				MainModel.getInstance().setComment((data as File), info.commentTextArea.text);
				MainModel.getInstance().setRecordName((data as File), info.recordNameTextInput.text);

				saveFlag = true;
			}
			
			popView();
		}
			
		private function popView():void
		{
			var v:SlideViewTransition = new SlideViewTransition();
			v.mode = SlideViewTransitionMode.UNCOVER;
			v.direction = ViewTransitionDirection.DOWN;
			this.navigator.popView(v);
		}
		
		public override function createReturnObject():Object{
			if (saveFlag)
			{
				// ファイル名を変更したらdataに入っているパスは古くなっているので、Fileインスタンスを作り直す。
				var file:File = MainModel.getInstance().getRecord(info.recordNameTextInput.text);
				return file;
			}
			else
			{
				// ファイル名を変更してないのならdataに入っているパスを使う
				return data;
			}
		}
		
		private function viewAddHandler(event:Event):void
		{
			info.currentState='edit';
			
			if (data != null) // アプリ再開がこの画面のときはdataはnullになっている
			{
				var file:File = data as File;
				info.recordNameTextInput.text = MainModel.getInstance().getRecordName(file);
				info.commentTextArea.text = MainModel.getInstance().getComment(file);
				info.recordNameTextInputChangeCallback = recordNameTextInputChangeHandler;
				doneButton.enabled = MainModel.getInstance().isValidRecordName(info.recordNameTextInput.text);
			}
		}
		
		private function recordNameTextInputChangeHandler(event:TextOperationEvent):void
		{
			doneButton.enabled = MainModel.getInstance().isValidRecordName(info.recordNameTextInput.text);
		}
	]]>
	</fx:Script>
	<s:navigationContent>
		<s:Button label="Cancel" click="cancelButtonClickHandler(event)"/>
	</s:navigationContent>
	<s:actionContent>
		<s:Button id="doneButton" label="Done" click="doneButtonClickHandler(event)" enabled="false"/>
	</s:actionContent>
	<components:RecordInformation id="info" gap="20" paddingBottom="10" paddingLeft="10"
								  paddingRight="10" paddingTop="10"/>
</s:View>
